// SPDX-License-Identifier: AGPL-3.0-or-later
pragma solidity 0.8.2;

import { IERC20 } from "@openzeppelin/contracts/token/ERC20/IERC20.sol";
import { SafeERC20 } from "@openzeppelin/contracts/token/ERC20/utils/SafeERC20.sol";
import { MassetHelpers } from "../../shared/MassetHelpers.sol";
import { AbstractIntegration } from "./AbstractIntegration.sol";
import { ICERC20 } from "./ICompound.sol";

/**
 * @title   CompoundIntegration
 * @author  mStable
 * @notice  A simple connection to deposit and withdraw bAssets from Compound and Cream
 * @dev     VERSION: 1.0
 *          DATE:    2021-05-04
 */
contract CompoundIntegration is AbstractIntegration {
    using SafeERC20 for IERC20;

    event SkippedWithdrawal(address bAsset, uint256 amount);
    event RewardTokenApproved(address rewardToken, address account);

    address public immutable rewardToken;

    /**
     * @param _nexus            Address of the Nexus
     * @param _lp               Address of liquidity provider. eg mAsset or feeder pool
     * @param _rewardToken      Reward token, if any. eg COMP
     */
    constructor(
        address _nexus,
        address _lp,
        address _rewardToken
    ) AbstractIntegration(_nexus, _lp) {
        rewardToken = _rewardToken;
    }

    /***************************************
                    ADMIN
    ****************************************/

    /**
     * @dev Approves Liquidator to spend reward tokens
     */
    function approveRewardToken() external onlyGovernor {
        address liquidator = nexus.getModule(keccak256("Liquidator"));
        require(liquidator != address(0), "Liquidator address is zero");

        MassetHelpers.safeInfiniteApprove(rewardToken, liquidator);

        emit RewardTokenApproved(rewardToken, liquidator);
    }

    /***************************************
                    CORE
    ****************************************/

    /**
     * @dev Deposit a quantity of bAsset into the platform. Credited cTokens
     *      remain here in the vault. Can only be called by whitelisted addresses
     *      (mAsset and corresponding BasketManager)
     * @param _bAsset              Address for the bAsset
     * @param _amount              Units of bAsset to deposit
     * @param isTokenFeeCharged    Flag that signals if an xfer fee is charged on bAsset
     * @return quantityDeposited   Quantity of bAsset that entered the platform
     */
    function deposit(
        address _bAsset,
        uint256 _amount,
        bool isTokenFeeCharged
    ) external override onlyLP nonReentrant returns (uint256 quantityDeposited) {
        require(_amount > 0, "Must deposit something");

        // Get the Target token
        ICERC20 cToken = _getCTokenFor(_bAsset);

        quantityDeposited = _amount;

        if (isTokenFeeCharged) {
            // If we charge a fee, account for it
            uint256 prevBal = _checkBalance(cToken);
            require(cToken.mint(_amount) == 0, "cToken mint failed");
            uint256 newBal = _checkBalance(cToken);
            quantityDeposited = _min(quantityDeposited, newBal - prevBal);
        } else {
            // Else just execute the mint
            require(cToken.mint(_amount) == 0, "cToken mint failed");
        }

        emit Deposit(_bAsset, address(cToken), quantityDeposited);
    }

    /**
     * @dev Withdraw a quantity of bAsset from Compound
     * @param _receiver     Address to which the withdrawn bAsset should be sent
     * @param _bAsset       Address of the bAsset
     * @param _amount       Units of bAsset to withdraw
     * @param _hasTxFee     Is the bAsset known to have a tx fee?
     */
    function withdraw(
        address _receiver,
        address _bAsset,
        uint256 _amount,
        bool _hasTxFee
    ) external override onlyLP nonReentrant {
        _withdraw(_receiver, _bAsset, _amount, _amount, _hasTxFee);
    }

    /**
     * @dev Withdraw a quantity of bAsset from Compound
     * @param _receiver     Address to which the withdrawn bAsset should be sent
     * @param _bAsset       Address of the bAsset
     * @param _amount       Units of bAsset to withdraw
     * @param _totalAmount  Total units to pull from lending platform
     * @param _hasTxFee     Is the bAsset known to have a tx fee?
     */
    function withdraw(
        address _receiver,
        address _bAsset,
        uint256 _amount,
        uint256 _totalAmount,
        bool _hasTxFee
    ) external override onlyLP nonReentrant {
        _withdraw(_receiver, _bAsset, _amount, _totalAmount, _hasTxFee);
    }

    function _withdraw(
        address _receiver,
        address _bAsset,
        uint256 _amount,
        uint256 _totalAmount,
        bool _hasTxFee
    ) internal {
        require(_totalAmount > 0, "Must withdraw something");
        require(_receiver != address(0), "Must specify recipient");

        // Get the Target token
        ICERC20 cToken = _getCTokenFor(_bAsset);

        // If redeeming 0 cTokens, just skip, else COMP will revert
        // Reason for skipping: to ensure that redeemMasset is always able to execute
        uint256 cTokensToRedeem = _convertUnderlyingToCToken(cToken, _totalAmount);
        if (cTokensToRedeem == 0) {
            emit SkippedWithdrawal(_bAsset, _totalAmount);
            return;
        }

        uint256 userWithdrawal = _amount;

        if (_hasTxFee) {
            require(_amount == _totalAmount, "Cache inactive with tx fee");
            IERC20 b = IERC20(_bAsset);
            uint256 prevBal = b.balanceOf(address(this));
            require(cToken.redeemUnderlying(_amount) == 0, "redeem failed");
            uint256 newBal = b.balanceOf(address(this));
            userWithdrawal = _min(userWithdrawal, newBal - prevBal);
        } else {
            // Redeem Underlying bAsset amount
            require(cToken.redeemUnderlying(_totalAmount) == 0, "redeem failed");
        }

        // Send redeemed bAsset to the receiver
        IERC20(_bAsset).safeTransfer(_receiver, userWithdrawal);

        emit PlatformWithdrawal(_bAsset, address(cToken), _totalAmount, _amount);
    }

    /**
     * @dev Withdraw a quantity of bAsset from the cache.
     * @param _receiver     Address to which the bAsset should be sent
     * @param _bAsset       Address of the bAsset
     * @param _amount       Units of bAsset to withdraw
     */
    function withdrawRaw(
        address _receiver,
        address _bAsset,
        uint256 _amount
    ) external override onlyLP nonReentrant {
        require(_amount > 0, "Must withdraw something");
        require(_receiver != address(0), "Must specify recipient");

        IERC20(_bAsset).safeTransfer(_receiver, _amount);

        emit Withdrawal(_bAsset, address(0), _amount);
    }

    /**
     * @dev Get the total bAsset value held in the platform
     *      This includes any interest that was generated since depositing
     *      Compound exchange rate between the cToken and bAsset gradually increases,
     *      causing the cToken to be worth more corresponding bAsset.
     * @param _bAsset     Address of the bAsset
     * @return balance    Total value of the bAsset in the platform
     */
    function checkBalance(address _bAsset) external view override returns (uint256 balance) {
        // balance is always with token cToken decimals
        ICERC20 cToken = _getCTokenFor(_bAsset);
        balance = _checkBalance(cToken);
    }

    /***************************************
                    APPROVALS
    ****************************************/

    /**
     * @dev Re-approve the spending of all bAssets by their corresponding cToken,
     *      if for some reason is it necessary. Only callable through Governance.
     */
    function reApproveAllTokens() external onlyGovernor {
        uint256 bAssetCount = bAssetsMapped.length;
        for (uint256 i = 0; i < bAssetCount; i++) {
            address bAsset = bAssetsMapped[i];
            address cToken = bAssetToPToken[bAsset];
            MassetHelpers.safeInfiniteApprove(bAsset, cToken);
        }
    }

    /**
     * @dev Internal method to respond to the addition of new bAsset / cTokens
     *      We need to approve the cToken and give it permission to spend the bAsset
     * @param _bAsset Address of the bAsset to approve
     * @param _cToken This cToken has the approval approval
     */
    function _abstractSetPToken(address _bAsset, address _cToken) internal override {
        // approve the pool to spend the bAsset
        MassetHelpers.safeInfiniteApprove(_bAsset, _cToken);
    }

    /***************************************
                    HELPERS
    ****************************************/

    /**
     * @dev Get the cToken wrapped in the ICERC20 interface for this bAsset.
     *      Fails if the pToken doesn't exist in our mappings.
     * @param _bAsset   Address of the bAsset
     * @return cToken   Corresponding cToken to this bAsset
     */
    function _getCTokenFor(address _bAsset) internal view returns (ICERC20 cToken) {
        address cTokenAddress = bAssetToPToken[_bAsset];
        require(cTokenAddress != address(0), "cToken does not exist");
        cToken = ICERC20(cTokenAddress);
    }

    /**
     * @dev Get the total bAsset value held in the platform
     *          underlying = (cTokenAmt * exchangeRate) / 1e18
     * @param _cToken     cToken for which to check balance
     * @return balance    Total value of the bAsset in the platform
     */
    function _checkBalance(ICERC20 _cToken) internal view returns (uint256 balance) {
        uint256 cTokenBalance = _cToken.balanceOf(address(this));
        uint256 exchangeRate = _cToken.exchangeRateStored();
        // e.g. 50e8*205316390724364402565641705 / 1e18 = 1.0265..e18
        balance = (cTokenBalance * exchangeRate) / 1e18;
    }

    /**
     * @dev Converts an underlying amount into cToken amount
     *          cTokenAmt = (underlying * 1e18) / exchangeRate
     * @param _cToken     cToken for which to change
     * @param _underlying Amount of underlying to convert
     * @return amount     Equivalent amount of cTokens
     */
    function _convertUnderlyingToCToken(ICERC20 _cToken, uint256 _underlying)
        internal
        view
        returns (uint256 amount)
    {
        uint256 exchangeRate = _cToken.exchangeRateStored();
        // e.g. 1e18*1e18 / 205316390724364402565641705 = 50e8
        // e.g. 1e8*1e18 / 205316390724364402565641705 = 0.45 or 0
        amount = (_underlying * 1e18) / exchangeRate;
    }
}
